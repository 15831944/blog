---
title: 使用esp8266制作远程空气质量监控系统
comments: true
tags: 
  - Arduino
  - Esp8266
category: Arduino
---





```cpp
#include <SSD1306.h>
#include <Ticker.h>
#include <SoftwareSerial.h>
#include <Arduino.h>
#include <ESP8266WiFi.h>
#include <WiFiClient.h>

#pragma region 全局配置变量
//wifi名和密码
char _ssid[20] = "REPLACE WITH YOURS";
char _pwd[20] = "REPLACE WITH YOURS";
//Onenet的apikey和deviceid
#define APIKEY "REPLACE WITH YOURS"
#define DEVICEID "REPLACE WITH YOURS"
#pragma endregion

#pragma region 无线
WiFiClient client;
int _wifiStatus = WL_IDLE_STATUS;
bool IsSSIDExist()
{
  int n = WiFi.scanNetworks();
  if (n == 0)
    return false;
  else
  {
    String ssid = String(_ssid);
    for (int i = 0; i < n; ++i)
    {
      Serial.println(WiFi.SSID(i));
      if (ssid.equals(WiFi.SSID(i)))
        return true;
      delay(10);
    }
  }
  return false;
}
bool ConnectToWifi()
{
  WiFi.mode(WIFI_STA);
  WiFi.begin(_ssid, (char *)"TKJOJKD");
  if (IsSSIDExist())
  {
    WiFi.begin(_ssid, _pwd);
    for (int i = 0; i < 25; i++)
    {
      delay(5000);
      Serial.print("Connecting to WPA SSID: ");
      Serial.println(_ssid);
      Serial.println(_pwd);
      Serial.println(_wifiStatus);
      if (WiFi.status() == WL_CONNECTED)
      {
        Serial.println("Wifi connected");
        _wifiStatus = WL_CONNECTED;
        return true;
      }
    }
  }
  _wifiStatus = WL_DISCONNECTED;
  return false;
}
#pragma endregion

#pragma region 屏幕
SSD1306 display(0x3c, 12, 14);
void PrintInitScreen()
{
  display.clear();
  display.setTextAlignment(TEXT_ALIGN_LEFT);
  display.setFont(ArialMT_Plain_24);
  display.drawString(0, 12, "WIFI");
  display.drawString(0, 36, "Connecting...");
  display.display();
}
void PrintResultScreen(String PM25, String PM10)
{
  display.clear();
  display.setTextAlignment(TEXT_ALIGN_LEFT);
  display.setFont(ArialMT_Plain_24);
  display.drawString(0, 12, PM25);
  display.drawString(0, 40, PM10);
  display.display();
}
#pragma endregion

#pragma region 攀藤传感器
int _pm2p5, _pm10;
SoftwareSerial SerialG5(13, 15);
void ReadSensorData()
{
  byte inbyte[30];
  byte state = 0;
  byte count = 0;
  while (true)
  {
    while (SerialG5.available() > 0)
    {
      byte inByte = SerialG5.read();
      switch (state)
      {
        case 0:
          if (inByte == 0x42)
            state = 1;
          break;
        case 1:
          state = (inByte == 0x4d) ? 2 : 0;
          count = 0;
          break;
        case 2:
          inbyte[count] = inByte;
          count++;
          break;
        default:
          state = 0;
          break;
      }
      if (count > 29)
        break;
    }
    if (count > 29)
    {
      int chkval = 143; //0x42+0x4d
      for (byte i = 0; i < 28; i++)
      {
        chkval += inbyte[i];
      }
      if (chkval == ((int)inbyte[28] * 256 + inbyte[29]))
      {
        Serial.println("OK");
        _pm2p5 = ((int)inbyte[10]) * 256 + inbyte[11];
        _pm10 = ((int)inbyte[12]) * 256 + inbyte[13];
        break;
      }
      else
      {
        Serial.println("error");
        state = 0;
        count = 0;
      }
    }
  }
}
#pragma endregion

#pragma region 定时器
Ticker flipper;
#define TICK 60 //60秒更新一次
bool _needUpdate = false;
void Flip()
{
  _needUpdate = true;
}
#pragma endregion

#pragma region 数据处理
#define LINEBREAK "\r\n"
void UpLoadtoOneNet(char *sensorID, int SensorData)
{
  if (client.connect("api.heclouds.com", 80))
  {
    String strval;
    int ilength = 56;
    Serial.println("connecting...");
    Serial.println(sensorID);
    Serial.println(SensorData);
    client.print("POST http://api.heclouds.com/devices/");
    client.print(DEVICEID);
    Serial.print(DEVICEID);
    client.print("/datapoints HTTP/1.1");
    client.print(LINEBREAK);
    client.print("Host: ");
    client.print("api.heclouds.com");
    client.print(LINEBREAK);
    client.print("api-key: ");
    client.print(APIKEY);
    client.print(LINEBREAK);
    client.println("Connection: close");
    client.print("Content-Length: ");
    strval = String(SensorData);
    ilength += strval.length();
    strval = String(sensorID);
    ilength += strval.length();
    client.print(ilength);
    client.print(LINEBREAK);
    client.print(LINEBREAK);
    client.print("{\"datastreams\":[{\"id\":\"");
    client.print(sensorID);
    client.print("\",\"datapoints\":[{\"value\":");
    client.print(SensorData);
    client.print("}]}]}");
    client.print(LINEBREAK);
    client.println(LINEBREAK);
  }
  else
  {
    Serial.println("connection failed");
    Serial.println();
    Serial.println("disconnecting.");
    client.stop();
  }
}
void SensorDataHandle()
{
  ReadSensorData();
  if (_wifiStatus == WL_CONNECTED)
  {
    UpLoadtoOneNet((char *)"_pm2p5", _pm2p5);
    delay(1000);
    UpLoadtoOneNet((char *)"_pm10", _pm10);
  }
}
#pragma endregion

void setup()
{
  display.init();
  display.flipScreenVertically();
  PrintInitScreen();
  Serial.begin(9600);
  SerialG5.begin(9600);
  if (ConnectToWifi())
  {
    flipper.attach(TICK, Flip);
    delay(3000);
    _needUpdate = true;
  }
}

void loop()
{
  if (_needUpdate)
  {
    Serial.println("Data Handle");
    SensorDataHandle();
    char ctmp[6];
    String PM25 = "PM2.5:";
    sprintf(ctmp, "%d", _pm2p5);
    PM25 += ctmp;
    String PM10 = "PM10:";
    sprintf(ctmp, "%d", _pm10);
    PM10 += ctmp;
    PrintResultScreen(PM25, PM10);
    _needUpdate = false;
  }
}
```